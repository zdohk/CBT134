SVC56LDR TITLE 'SVC56LDR - INSTALL SVC 56 FRONT-END '
*
*        LOAD A FRONT-END TO SVC 56 (ENQ/RESERVE)
*
*        WRITTEN 18TH DECEMBER 2000
*        BY GREG PRICE
*        HANSEN CORPORATION
*
         YREGS
         EJECT
SVC56LDR CSECT
SVC56LDR AMODE 31
SVC56LDR RMODE 24
*
         USING SVC56LDR,R15
         B     STARTLDR
         DC    AL1(L2-L1)
L1       DC    C'SVC56LDR &SYSDATC'
L2       EQU   *
         CNOP  0,4
STARTLDR EQU   *
*
*  INITIALISATION
*
         STM   R14,R12,12(R13)
         LR    R12,R15
         DROP  R15
         USING SVC56LDR,R12
         ST    R13,SAVE+4
         LA    R2,SAVE
         ST    R2,8(0,R13)
         LR    R13,R2
*
*  LOCATE THE SVC TABLE
*
         L     R3,CVTPTR           POINT TO THE CVT.
         USING CVT,R3
         L     R4,CVTABEND         POINT TO THE SECONDARY CVT.
         DROP  R3                  CVT.
         USING SCVTSECT,R4
         L     R10,SCVTSVCT        POINT TO THE SVC TABLE.
         DROP  R4                  SCVTSECT.
*
*  INSTALL SVC56FE AS FRONT-END FOR SVC 56
*
         MODESET KEY=ZERO,MODE=SUP
         ENQ   (UQNAME,URNAME,E,5,SYSTEM),RET=NONE
         LA    R9,56*8(,R10)       POINT TO TABLE ENTRY FOR SVC 56.
         CLC   =X'80000000',4(R9)  EXPECTED SVC ATTRIBUTES?
         BNE   BADFE056            NO, CANNOT INSTALL FRONT-END.
         LOAD  EPLOC=SVC56FE       LOAD THE LATEST VERSION.
         LR    R3,R0               POINT TO THE FRESH COPY.
         L     R4,0(,R9)           POINT TO THE EXISTING COPY.
         CLC   0(16,R3),0(R4)      BOTH HAVE SAME SIZE AND DATE?
         BE    NOPFE056            YES, MODULE IS ALREADY INSTALLED.
         L     R0,4(,R3)           GET SIZE OF FRESH COPY.
         LR    R4,R3               COPY THE SOURCE ADDRESS.
         LR    R5,R0               COPY MODULE SIZE.
         GETMAIN RU,LV=(0),SP=228,LOC=ANY
         LR    R14,R1              POINT TO THE NEW AREA.
         LR    R15,R5              COPY MODULE SIZE.
         MVCL  R14,R4              COPY THE MODULE INTO COMMON STORAGE.
         LR    R3,R1               POINT TO NEW COMMON MODULE.
         LA    R0,1
         SLL   R0,31               SET SIGN BIT ON
         OR    R3,R0               SET AMODE=31 BIT ON
         MVC   16(4,R3),0(R9)      SET "REAL" SVC FORWARD POINTER.
         SVCUPDTE 56,REPLACE,TYPE=2,EP=(R3),APF=NO
         WTO   'SVC56LDR FE56 INSTALL COMPLETE',                       +
               ROUTCDE=(2,11)
         B     DELFE056            MODULE IS NOW NEWLY INSTALLED.

NOPFE056 WTO   'SVC56LDR FE56 WAS ALREADY INSTALLED',                  +
               ROUTCDE=(2,11)

DELFE056 DELETE EPLOC=SVC56FE      DELETE THE PRIVATE COPY.
         B     DEQFE056            SVC FRONT-END NOW INSTALLED.

BADFE056 WTO   'SVC56LDR ATTRIBUTE MISMATCH - FE56 NOT INSTALLED',     +
               ROUTCDE=(2,11)

DEQFE056 DEQ   (UQNAME,URNAME,5,SYSTEM),RET=NONE
         MODESET KEY=NZERO,MODE=PROB
*
*  TERMINATE AND EXIT
*
         L     R13,4(,R13)
         LM    R14,R12,12(R13)
         SLR   R15,R15
         BR    R14
         EJECT
         DS    0D
UQNAME   DC    CL8'SYSZSVC'
URNAME   DC    CL8'TABLE'
SVC56FE  DC    CL8'SVC56FE '
         DC    CL8'SAVEAREA'
SAVE     DC    18F'0'
*
         LTORG
*
         DS    0D
         DC    CL8'ENDOFLDR'
         DS    0D                  END OF CSECT.
         EJECT
         PRINT NOGEN
*
         CVT   DSECT=YES
*
         IHASCVT
*
         END   SVC56LDR
