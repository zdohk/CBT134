HANSVC56 TITLE ' HANSVC56  ENQ/RESERVE SVC - 56  FRONT END '
*
*        LOAD A FRONT-END TO SVC 56 (ENQ/RESERVE)
*
*        WRITTEN 18TH DECEMBER 2000
*        BY GREG PRICE
*        HANSEN CORPORATION
*
*
*   MAINTENANCE ACTIVITY
*    18 DEC 00  - ORIGINAL VERSION
*
*   FUNCTION  -  SVC 56 FRONT END DESIGNED TO TRAP
*                ENQUEUES WITH A MAJOR NAME OF 'SYSIKJUA'
*                AND CONVERT THEM TO SHARED TO ALLOW
*                MULTIPLE TSO SESSIONS FOR A TSO USERID.
*
*   INPUT REGISTERS  -
*         R0:    USER DATA
*         R1:    USER DATA
*         R2:    N/A
*         R3:    CVT ADDRESS
*         R4:    TCB ADDRESS
*         R5:    SVRB ADDRESS
*         R6:    ENTRY POINT ADDRESS
*         R7:    ASCB ADDRESS
*         R8-12: N/A
*         R13:   USER DATA
*         R14:   RETURN ADDRESS
*         R15:   USER DATA
*
*   ENTERED BY  -  SVC 56
*
*   MACROS USED
*    YREGS, IHARB
*
*   ATTRIBUTES  -  RENT
*
*   SVCS ISSUED - NONE
*
*   RETURN CODES - NOT SET BY THIS CODE
*
*   MODULES CALLED - NONE
*
*   MESSAGES ISSUED - NONE
*
         EJECT
HANSVC56 CSECT
HANSVC56 AMODE 31
HANSVC56 RMODE ANY

         YREGS

         USING HANSVC56,R6
         USING RBBASIC,R5
FESTART  B     L001                BRANCH AROUND MODULE HEADER.

         DC    AL4(FEEND-FESTART)  SIZE OF MODULE.
         DC    CL8'&SYSDATC'       ASSEMBLE DATE.
FORWARD  DC    A(0)                PREVIOUS SVC ENTRY POINT.

LGOFORTH LM    R14,R9,RBEXSAVE     RESTORE ORIGINAL SVC REGISTERS.
         L     R6,FORWARD          GET TRUE SVC EP ADDRESS
         BSM   0,R6                AND GO AWAY.


         DC    C'HANSVC56 18DEC2000'
         CNOP  0,4

L001     STM   R14,R9,RBEXSAVE     SAVE MOST REGS IN SVRB.
*
*  SVC56 PARMLIST FROM MVS DIAGNOSIS: REFERENCE
*
*
* R15 AND R0    NO APPLICABLE DATA.
* R1            ADDRESS OF THE PARAMETER LIST.
* PLIST         36 BYTES OF THE ENQ/RESERVE PARAMETER LIST,
*               REPRESENTING AN ENQ REQUEST FOR A SINGLE RESOURCE.
*               THE COMPLETE PARAMETER LIST CAN INCLUDE REQUESTS FOR
*               ADDITIONAL RESOURCES; THE LAST REQUEST IS IDENTIFIED
*               BY A FLAG BIT DESCRIBED BELOW.
*
*               BYTES
*               -20    RESERVED.
*               -16    IF AN MASID ENQ AND AN ECB IS SPECIFIED,
*                      CONTAINS THE ECB ADDRESS.
*               -12    IF AN MASID ENQ, CONTAINS THE MASID OPERAND
*                      VALUE.
*               -8     IF AN MASID ENQ, CONTAINS THE MTCB OPERAND
*                      VALUE; OTHERWISE, CONTAINS THE TCB ADDRESS IF
*                      BOTH THE TCB AND THE ECB ARE SPECIFIED.
*               -4     IF AN MASID ENQ, CONTAINS THE FORMAT WORD OF
*                      DECIMAL 20; OTHERWISE, CONTAINS THE TCB OR THE
*                      ECB ADDRESS DEPENDING ON WHETHER TCB= OR ECB=
*                      WAS SPECIFIED. (SEE THE FLAG BYTES FOLLOWING.)
*               0      FLAG BITS AS FOLLOWS:
*
*                      ....    .0..     RNL=YES
*                      1...    ....     END-OF-LIST INDICATOR; IF
*                                       ZERO, THE PARAMETER LIST
*                                       INCLUDES ANOTHER RESOURCE
*                                       REQUEST.
*                      .1..    ....     OLD OPTIONS ARE IN EFFECT
*                                       (BITS HAVE NO MEANING).
*                      .01.    ....     INDICATES LOC=ANY SPECIFIED
*                                       ON A RESERVE REQUEST. THE
*                                       UCB MAY RESIDE IN 31-BIT
*                                       STORAGE.
*                      .0.1    ....     INDICATES THAT THE
*                                       REQUESTOR OF THE RESOURCE
*                                       NOW OWNS THE RESOURCE AND
*                                       THE RESOURCE IS SHARED. IF
*                                       ZERO, THE RESOURCE IS OWNED
*                                       EXCLUSIVELY.
*                      .0..    1...     FORMAT WORD INDICATOR. IF
*                                       ZERO, THE LIST OF PELS DOES
*                                       NOT HAVE THE MASID FORMAT
*                                       PREFIX.
*                      .0..    ...1     "TCB=TCBADDR" WAS
*                                       REQUESTED; PARMLIST PREFIX
*                                       CONTAINS THE TCB ADDRESS.
*
* 1             LENGTH OF THE MINOR NAME WHOSE ADDRESS IS IN BYTES 8
*               - 11 OF THIS ELEMENT.  ZEROS INDICATES THE LENGTH OF
*               MINOR NAME IS IN THE FIRST BYTE OF THE MINOR NAME
*               FIELD WHOSE ADDRESS IS IN BYTES 8 - 11 OF THIS
*               ELEMENT (DOES NOT INCLUDE THE LENGTH BYTE ITSELF).
*
* 2             ENQ PARAMETERS BYTE; BIT SETTINGS ARE:
*               BITS
*
*               0...    ....     EXCLUSIVE REQUEST.
*               1...    ....     SHARED REQUEST.
*               .0..    0...     SCOPE OF THE MINOR NAME IS
*                                STEP.
*               .0..    1...     RESERVE TYPE. THE RESOURCE
*                                IS KNOWN ACROSS SYSTEMS AND
*                                UCB= WAS SPECIFIED. THE
*                                LAST WORD OF THE PARAMETER
*                                LIST IS THE ADDRESS OF A
*                                WORD CONTAINING THE UCB
*                                ADDRESS.
*               .1..    0...     SCOPE OF THE MINOR NAME IS
*                                SYSTEM.
*               .1..    1...     SCOPE OF THE MINOR NAME IS
*                                SYSTEMS.
*               ..1.    ....     OBSOLETE.
*               ...1    ....     SET "MUST COMPLETE" EQUAL
*                                TO STEP.
*               ....    .000     RET=NONE.
*               ....    .001     RET=HAVE.
*               ....    .010.    RET=CHNG.
*               ....    .011.    RET=USE.
*               ....    .100     "ECB=ADDR". THE ECB ADDRESS
*                                IS CONTAINED IN THE
*                                PARAMETER LIST PREFIX.
*               ....    .111     RET=TEST.
*
* 3             FIELD FOR CODES RETURNED TO THE ISSUER BY ENQ.
*
* 4-7           ADDRESS OF THE MAJOR RESOURCE NAME (QNAME).
*
* 8-11          ADDRESS OF THE MINOR RESOURCE NAME (RNAME).
*
* 12-15         IF BITS 1 AND 4 OF THE ENQ PARAMETER BYTE ARE SET TO
*               0 AND 1 RESPECTIVELY, THIS WORD CONTAINS THE ADDRESS
*               OF A WORD CONTAINING THE UCB ADDRESS; OTHERWISE, THE
*               CONTENT OF THIS WORD WILL BE ZERO.
*
* NOTE:  RESERVE IS BASICALLY AN ENQ WITH UCB= SPECIFIED. SEE FLAG
*        BYTE 2 ABOVE.
*
* REGISTER CONTENTS ON RETURN (PROVIDED ONLY IF RET=TEST, RET=USE,
* RET=CHNG, OR RET=HAVE):
*
* R15       00 IF EACH RETURN CODE FOR EACH RESOURCE NAMED IN ENQ IS 0.
*           OTHERWISE, R15 CONTAINS THE ADDRESS OF A STORAGE AREA
*           CONTAINING THE RETURN CODES.
*
         LR    R11,R1              GET A(PARMLIST)
         L     R10,RBLINK          POINT TO PRB
         N     R10,HIOFF           TURN OFF HI BYTE
         DROP  R5                  RBBASIC
         USING RBBASIC,R10         R10 POINTS TO CALLERS RB
         TM    RBOPSWA,RBOPSWM     TEST ADDRESSING MODE OF CALLERS PSW
         BO    L005
         DROP  R10                 RBBASIC
         USING RBBASIC,R5          R5 POINTS TO SVRB
         N     R11,HIOFF           TURN OFF HI BYTE BITS, CALLER
*                                  WAS IN 24-BIT ADDRESSING MODE
L005     EQU   *
         N     R11,X7FFF           TURN OFF SIGN BIT
         BZ    LGOFORTH            IGNORE ZERO POINTER
         TM    2(R11),X'80'        SHARED ENQUEUE?
         BO    LGOFORTH            YES, DO NOTHING HERE
         TM    2(R11),X'40'        SCOPE=STEP OR RESERVE?
         BNO   LGOFORTH            YES, DO NOTHING HERE
         L     R9,4(0,R11)         GET A(MAJOR NAME)
         N     R9,X7FFF            TURN OFF SIGN BIT
         BZ    LGOFORTH            IGNORE ZERO POINTER
         CLC   =C'SYSIKJUA',0(R9)  TSO ID ENQ?
         BNE   LGOFORTH            NO, DO NOTHING HERE
         OI    2(R11),X'80'        YES, CONVERT TO SHARED
         B     LGOFORTH            CHANGE NOW COMPLETE
*
HIOFF    DC    0F'0',XL4'00FFFFFF'
X7FFF    DC    0F'0',XL4'7FFFFFFF'
         LTORG
         CNOP  0,8
         DC    CL8'E FE056'
FEEND    EQU   *
*
         EJECT
         IHARB
         END   HANSVC56
